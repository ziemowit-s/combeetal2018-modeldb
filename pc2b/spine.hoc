// Passive properties from Migliore 2005
Vrest = -65
celsius = 35.0
RmDend = 28000
RaAll= 150

CmDend = 1
caR_spine = 0.03
caR_vact = -30	// make half-activation point more hyperpolarised
caR_tinact = 20	// make inactivation faster
mykca_spine = 0.001

// From Graham et al 2014
// Spine dimesions
// approximate Grunditz et al model
//sneck_diam = 0.04 /* um (high resistance) */
spine_neck_diam = 0.125 /* um (low resistance) */
spine_neck_len = 1.0 /* um */
spine_head_diam = 0.5 /* um */
spine_head_len = 0.5 /* um */

proc attach_spine() {
    // assume that concrete section is accessed when proc is call
    // $o1 - spine_neck
    // $o2 - spine_head
    // $3 - rel
    $o1.sec {
        L = spine_neck_len
        diam = spine_neck_diam

        insert pas
        e_pas=Vrest
        g_pas=1/RmDend
        Ra=RaAll
        cm=CmDend
        insert cad
        taur_cad=14
        insert ds
        insert dca
    }
    $o2.sec {
        L = spine_head_len
        diam = spine_head_diam
        insert pas
        e_pas=Vrest
        g_pas=1/RmDend
        Ra=RaAll
        cm=CmDend
        insert carF
        gcabar_carF=caR_spine
        vha_carF=caR_vact
        ti_carF=caR_tinact
        insert mykca
        gkbar_mykca = mykca_spine
        insert cad
        taur_cad=14
        depth_cad=spine_head_diam/2
        insert ds
        insert dca
    }

    // connect head to neck
    $o1.sec connect $o2.sec(0), 1
    // connect neck to section
    connect $o1.sec(0), $3
    // loc(0.5) - access to 'this' section, which is accessed before proc is called
    //$o1.sec loc(0.5)
}